---
tags:
title: Circom
#date: 2020-11-05 20:45:59
#published: false
permalink: circom
#sidebar:
#    nav: cryptomat
#article_header:
#  type: cover
#  image:
#    src: /pictures/.jpg
---

{: .info}
**tl;dr:** My current sense: `circom` is still in its early days.
First, it lacks an ability to write correctness tests natively in its own language (as opposed to JavaScript testing frameworks).
Second, it gives no mechanism for developers to ascertain soundness of their templates.
(In its defense, the only such mechanism is a formal verification framework.)
Third, it has not been designed for safety (e.g., weird comparison semantics; a lack of types, at least initially; no testing framework in its own language).

<!--more-->

<!-- Here you can define LaTeX macros -->
<div style="display: none;">$
$</div> <!-- $ -->

## Gotchas

 - **Cannot test correctness** of your circom code without special tools like `circom_tester` or `circom_kit`
    + e.g., does my [witness generation logic](#witness-generation-versus-constraints) produce a satisfying assignment?
 - **Impossible** to **test soundness** of your circom code
    + e.g., does this malicious assignment pass my constraints?
    + just try and write such a test for the [IsZero template](#iszero-template)
 - **Comparison operators** (`<`, `>`, `>=`, `<=`) on `var`'s treat $\Zp$ values as **signed**.
    + This will daze and confuse you, at best.
    - At worst, it'll cause soundness issues.
 - Cannot declare a constant (e.g., `var MAX_LEN = 32;`) and then declare an array of that size (e.g., `var array[MAX_LEN];`)
 - Cannot out-of-the-box test that $\ell$ bits "fit" into a $\Zp$ field element, where $p$ is `circom`'s chosen prime
    - Even worse, you may not even understand why it's often necessary to ensure this!
    + This can create soundness issues (e.g., the `LessThan` template in `circomlib` is hardcoded for BN254's prime $p$, but what if the chosen $p$ is much smaller?)
    + Fortunately, there are [ways to do it manually](#assertbitsfitscalarn-compile-time-check).
 - The `LessThan` template is only sound when its inputs are `N` bits: beware and [read this](#lessthann-template)!
 + `circomlib` doesn't use safety features like _tags_ or _buses_
    + This would completely avoid issues like `LessThan` above

## Notes

As put by Palladino[^Pall23]:
> The main challenges [in] Circom is understanding when you are writing constraint-generation code, and when you are writing witness-generation code, and tracking what values are known at compile-time versus only known at runtime.
> [...]
> Keeping in mind the difference between constraint and witness generation times can guard you against underconstrained computation bugs.

### Terminology

 - **witness generation**: computation of _hints_ a.k.a. "witness data" that aids in satisfying the constraints encoded in a circom template
    - can be sped up with [parallel](https://docs.circom.io/circom-language/templates-and-components/#components) keyword
 - **variables**
    - [circom docs say](https://docs.circom.io/circom-language/variables-and-mutability/): _"variables are identifiers that hold non-signal data and are mutable."_
    - but, a more accurate mental model, AFAICT, is that they are compile-time symbolic expressions (e.g., often they are used to symbolically-store a linear combination of signals at compile time; see the [Num2Bits example](#num2bitsn-template))
 - **functions**: reusable pieces of code used only during _witness generation_
 - **signals**
    - input signals; i.e.,:
        + part of the NP relation's statement or witness
        + you cannot do `s <== expression` for `signal input s`; only for `signal s`.
    - output signals
    - intermediate signals (computed via witness generation)
 - **template**: a family of circuits, parameterized by one or more values known at compile-time (think C++ templates)
    + template parameters
 - components
    - [anonymous components](https://docs.circom.io/circom-language/anonymous-components-and-tuples/)
 - **assignment**
 - **constraints**

### Operators

Source [here](https://docs.circom.io/circom-language/basic-operators/).

 - `a \ b` computes the $\lfloor a / b\rfloor$ division quotient as if dividing natural numbers (i.e., s.t. $a=b q + r$ and $0 \le r < b$, when viewed as natural numbers)
 - `a % b` similarly computes the remainder $r$ 
 - all other binary operators perform arithmetic modulo the R1CS prime $p$, as one would expect, **except** for the [comparison operators](#comparison-operators-----on-vars), as already mentioned in the [gotcha's above](#gotchas)

### Signals

#### Input signals versus output signals.

Marking a `signal` in a template as `input` or `output` defines a "function-like" interface for the template.

This allows for a nicer calling syntax that avoids explicitly instantiating a template via the `component` keyword.
(See [anonymous components](https://docs.circom.io/circom-language/anonymous-components-and-tuples/) in the circom docs.)

### The main component 

An example:
```
pragma circom 2.0.0;

template Multiplier2() {
   signal input in1;
   signal input in2;
   signal output out <== in1 * in2;
}

component main { public [in1, in2] } = Multiplier2();
```

As emphasized in the circom docs: **"all output signals of the main component are public (and cannot be made private)"**.

In contrast, _"the input signals of the main component are private if not stated otherwise using the keyword public as above"_

### Variables: `var` versus `signal`

Initially, `var` seemed like it was just a _"compile-time derivable linear combination of signals"_ (including the trivial one like `var = some_signal;` or `var = SomeTemplateCall(N)(in);`).

A good example of this is in the [Num2Bits template](#num2bitsn-template).

But you can also put (compile-time) constants or any other compile-time expression.

{: .warning}
Most importantly, in `circom`, compile-time constants/values stored in `var`'s are stored as $\Z_p$ scalar field elements!
So if you add or multiply too much, you will "wrap around": e.g., $p/2 + p/2 = p = 0$.

### Comparison operators (`<`, `>`, `>=`, `<=`) on `var`'s

{: .warning}
Even more importantly, `circom` treats these $\Zp$ values inside `var`s as **signed** integers when doing comparisons!
This will trip you up!

 - Everything in $v\in [0, p/2]$ is treated as positive; so, as expected, its actual value is $\bar{v} = v \in [0,p/2]$.
 - But: everything in $v\in [p/2 + 1, p)$ is treated as _negative_, which is unexpected: 
its actual value, from the point of view of the comparison operator, is $\bar{v} = v - p \in [-(p/2 - 1), -1])$.

See [here](https://docs.circom.io/circom-language/basic-operators/#relational-operators) for details.

### Witness generation versus constraints

{: .todo}
Find a more natural circuit that showcases this difference. Not `IsZero()`.

As explained by Palladino[^Pall23]:

 > When writing a circuit you're writing two different programs, that belong to two different programming paradigms, in a single one.

The most important thing to know: [when (not) to use `<--`](https://github.com/pluto/circom-correctly-constrained?tab=readme-ov-file#when-may-a-developer-choose-to-use----assignment-over-).

### Tags

[Tags](https://docs.circom.io/circom-language/tags/) seem very useful for safety. 
An article by Hess[^Hess24] explains them well.
Unfortunately, nobody seems to use them.

e.g., the output signal `out` of the [IsZero](#iszero-template) template could be tagged as `binary` to indicate that the returned output is either 0 or 1.

{: .note}
I found Circom's [docs](https://docs.circom.io/circom-language/tags/) confusing because they make it seem like `binary` is a language-reserved tag with this actual meaning.
(e.g., _"This tag means that each signal in the array is always expected to be 0 or 1, in order to compute the corresponding number correctly."_)
But it is not: there are no "reserved" circom tags.

The code below would propagate the `binary` tag to the `a` signal:
```
signal a <=== IsZero()(10);
```

Tags can have compile-time values:

 > "Valued tags behave like parameters which means that they can only be assigned to values known at compilation time."

e.g., a template's compile time parameter can be used to tag its output signal's max bit length:

```
template Bits2Num(n) {
    signal input {binary} in[n];
    signal output {maxbit} out;
    var num = 0;

    var pow_2 = 1;
    for (var i = 0; i < n; i++) {
        num += in[i] * pow_2;
        pow_2 = pow2 + pow_2;
    }
    out.maxbit = n;

    num ==> out;
}
```

This suggests max lengths of strings could be tags.

### Buses

Very useful for readable, maintainable code.
And therefore, for safety.
See [here](https://docs.circom.io/circom-language/buses/#definition).

Buses, combined with [tags](#tags) should offer a decent amount of type-safety in `circom`.
(Yet nobody seems to use buses either.)

For example, an elliptic curve point can be represented as:

```
bus Point() {
    signal x;
    signal y;
}
```

You can even tag buses: e.g., an Edwards `Point()` vs. a Montgomery one!
So, you can have templates that work like this:

```
template Edwards2Montgomery () {
  input  Point() { edwards_point    } in;
  output Point() { montgomery_point } out;

  out.x <â€“- (1 + in.y) / (1 - in.y);
  out.y <â€“- out.x / in.x;

  out.x * (1 - in.y) === (1 + in.y);
  out.y * in.x === out.x;
}
```

Or, you can even tag signals in buses:

``` 
bus Book () {
  signal {maxvalue} title[50];
  signal {maxvalue} author[50];
  signal {maxvalue} sold_copies;
  signal {maxvalue} year;
};
```

Buses can be nested (but not recursed):

```
bus Date() {
    signal day;
    signal month;
    signal year;
}

bus Person() {
    signal name[50];
    Date() birthday;
}
```

Buses can be parameterized by compile-time constants:

```
bus Vector(N) {
    signal v[N];
}

bus Matrix(NUM_ROWS, NUM_COLS) {
	Vector(NUM_ROWS) column[NUM_COLS];
}
```

### Assert

The `assert` keyword is particularly "interesting."
I think I'm gonna stay away from it.

Quoting from [here](https://github.com/pluto/circom-correctly-constrained?tab=readme-ov-file#the-basics), it is supposed to add constraints:
```
// if all values are known at compile-time the assertion is checked then
// otherwise, the assertion creates a constraint.
assert(a <= b);
assert(a * a == b);
```

But I don't think that's true, from looking at the circom docs and from [here](https://www.rareskills.io/post/circom-tutorial#:~:text=constraint%20to%20pass.-,Circom%20assert,-Confusingly%20enough%2C%20Circom) or [here](https://www.chainsecurity.com/blog/circom-assertions-misconceptions-and-deceptions): it does **not** add constraints.


## Non-trivial examples

### `IsZero()` template

This is a template that outputs 1 when the input signal is 0 and outputs 0 otherwise.

It should be the most trivial thing to implement, no?
Yet it turns out that, while checking whether something is zero is trivial (i.e., `something === 0`), assigning a signal with the truth value of `something === 0` is not as trivial...
(This is because the only operations in `circom` are equality checks, addition and multiplication.)

{: .note}
This example will also illustrate the difference between _witness generation_ versus _constraining_.

```
template IsZero() {
  signal input in;
  signal output out;
  signal inv;

  inv <-- in != 0 ? 1/in : 0;   // W1 (witgen one)

  out <== -in * inv + 1;  // C1 (constraint one)
  in * out === 0;         // C2 (constraint two)
}
```

How does it work? Here's a simple analysis:

 - When `in` is 0, then:
    - `out <== -in * inv + 1` (`C1`) implies `out <== 0 * inv + 1`.
	+ Which implies `out <== 1`, ensuring the desired behaviour.
	- **Q**: But then `inv` does not even matter for this case? Which is why it is assigned 0? But why 0? It could be assigned anything it seems.
 - When `in` is **not** 0, then:
    - `in * out === 0` (`C2`) implies `out === 0`. 
	+ As a result, `out <== -in * inv + 1` (`C1`) implies `0 <== -in * inv + 1`
	+ Which implies `in * inv === 1`
	- Which can be satisfied, since the witness generation can compute an inverse via the `inv <-- in != 0 ? 1/in : 0` line.

{: .error}
**Observation:** I find the explanation in [`circom`'s docs](https://docs.circom.io/circom-language/basic-operators/#examples-using-operators-from-the-circom-library) a bit confusing, perhaps even in a dangerous way.
It says: _"If `in` is 0, then [...]. Otherwise, `in * inv` is always 1, then `out` is 0."_
This could be interpreted to suggest that `circom` enforces that `inv` be correctly computed as the inverse of `in`. Which is not the case. 
It's not that circom guarantees `in * inv` will be always 1; it's that when `in` is _not_ zero, the only way to satisfy the `C1` and `C2` constraints is to correctly compute the correct inverse `inv` of `in` (via the witness generation code at `W1`)

{: .note}
In a way, this rather-elegant `IsZero()` template perfectly illustrates how non-intuitive `circom` can be when it comes to efficiently-programming ZK relations.

### `Num2Bits(N)` template

Let's also consider the `Num2Bits()` [example from circom's docs](https://docs.circom.io/circom-language/basic-operators/#examples-using-operators-from-the-circom-library), which returns the $n$-bit binary representation of its input.

This example illustrates:
 - template parameters (e.g., `n`)
 - _variables_ versus _signals_
 - _witness generation_ versus _constraining_

```
template Num2Bits(N) {
    signal input num;
    signal output {binary} bits[N];

    // Asserts that 2^N - 1 fits in a scalar
    _ = AssertBitsFitScalar(N);

    // incrementally-updated to eventually store the symbolic expression:
    //
    //   bits[0] * 1 + bits[1] * 2 + bits[2] * 2^2 + ... + bits[N-1] * (2^(N-1))
    //
    var acc = 0;

    // stores increasing powers of two: 2^0, 2^1, 2^2, ...
    var pow2 = 1;

    for (var i = 0; i < N; i++) {
        bits[i] <-- (num >> i) & 1;    // extracts `num`'s ith bit
        bits[i] * (bits[i] - 1) === 0; // constrains bits[i] to be a bit

        // appends `+ bits[i] * 2^i` as a term to the symbolic expression in
        // `acc`, setting it to \prod_{i = 0}^{N-1} bits[i] * 2^{i+1}
        acc += bits[i] * pow2;

        // set to 2^{i+1}
        pow2 = pow2 + pow2;
    }

    // since acc stores the symbolic expression from above, this constrains
    // that the `bits` are indeed the binary representation of `num`
    num === acc;
}
```

{: .note}
It was actually ChatGPT who helped me understand that `acc` merely stores a symbolic expression that is derivable at compile-time (see [here](https://chatgpt.com/share/67d70727-0e14-8001-9831-a003bb53aa57)), because circom docs, IMHO, do a poor job at clarifying the distinction between circom _signals_ and circom _variables_.


### `LessThan(N)` template

{: .warning}
This template **must not** be used for $a < b$ range checks unless $a$ and $b$ have already had `N`-bit range checks done.
See an example of a bug [here](https://github.com/0xPARC/zk-bug-tracker?tab=readme-ov-file#4-mismatching-bit-lengths).

{: .todo}
Give a soundness break example.

```
/**
 * Outputs a truth bit for whether in[0] < in[1] when viewed as N-bit 
 * integers.
 *
 * Parameters:
 *   N      in[0] and in[1] MUST be in [0, 2^N)
 *
 * Input signals:
 *   in     array of the two numbers to be compared
 *
 * Output signals:
 *   out    a bit indicating whether in[0] < in[1] when viewed as integers 
 */
template LessThan(N) {
    // Do not worry about this, for now (discussed later)
    _ = AssertBitsFitScalar(N);

    signal input in[2];
    signal output out;  // TODO(Tag): add {binary} tag?

    // (N+1)-bit representation of m = (2^N + in[0]) - in[1]
    // e.g., for N = 3
    //  in[0] = 100
    //  in[1] = 001
    //  m     = (1000 + 100) - 001
    //        = 1100 - 001
    //        = 1011
    signal bits <== Num2Bits(N + 1)(in[0] + (1 << N) - in[1]);

    // if in[0] >= in[1], then:
    //   bits[N], the nth bit toggled above by adding 2^N, remains 1 => out is 0
    // else:
    //   bits[N] turns into 0 => out is 1
    out <== 1 - bits[N];
}
```

### `DivRem()` template

It's surprisingly tricky to implement a basic "division with remainder" in circom because emulating integer division in a finite field is rife with dangers.
For [example](https://github.com/privacy-scaling-explorations/zk-kit.circom/issues/14#issuecomment-2598589492):

Normally, for any $a,b\in \N$, Euclid's division theorem says there exist **unique** $q,r\in\N$ s.t.:
\begin{align}
a = q\cdot b + r\\\\\
0\le r < b
\end{align}

Now, suppose we are implementing a circom template that checks $a,b,q,r$ for the properties above via:
```
a === b * q + r;
_ <== Num2Bits(M)(r);
is_valid_remainder <== LessThan(M)([r, b]); // assume b < 2^M
is_valid_remainder === 1;
```

Are we good?
Is this enough to guarantee uniqueness of $q$ and $r$?

**Nope!** 

{: .warning}
AFAICT, some [online resources are wrong on this](https://0xparc.org/blog/zk-ecdsa-2#:~:text=BigInt%20Division/Modulo), suggesting that simply checking $r < b$ is enough.

If the field were $\F_{13}$, then dividing $a=10$ by $b=3$ we would have two solutions:
\begin{align}
q = 3 \wedge r = 1\Rightarrow
(q \cdot b + r) \bmod {13} = (3\cdot 3 + 1) \bmod {13}= 10 \bmod {13} = a
\end{align}
But also:
\begin{align}
q = 7 \wedge r = 2\Rightarrow
(q \cdot b + r) \bmod {13} = (7\cdot 3 + 2) \bmod {13} = 23 \bmod {13} = 10 \bmod {13} = a
\end{align}

To fix this, we actually have to check that quotient $q$ is bounded too.
There may be several ways of doing this, some more efficient than others.

Below, I describe an approach that, assuming $0\le a < 2^N$ and $0 < b < 2^M$ simply checks if $q < 2^N$.
This guarantees soundness as long as $2^M(2^N + 1) \le p$, because:
 - We know that $a < 2^N$ and $b < 2^M$.
 - We will check that $a \equiv q b + r \pmod p$
 - We will check that $0 \le r < b$
 - We will check that $0 \le q < 2^N$
 - **We are good** as long as $qb + r < p$ for all such $(b,q,r)$
    + i.e., the Euclidean division check mod $p$ from above will hold over the integers
 - From above, we can bound $q b + r < 2^{N + M} + 2^M$
 - So, as long as $2^{N + M} + 2^M \le p$, we are good
 - Equivalently, as long as $2^M(2^N + 1) \le p$, we are good

{: .note}
The code below leverages [tags](#tags).
It also uses [AssertBitsFitScalar](#assertbitsfitscalarn-compile-time-check) (defined later) and [LessThan](#lessthann-template), but modified to take args individually, not as an array.
To understand the code better, see it directly in `circom-stdlib`[^circom-stdlib].

```
template DivRem() {
    signal input {maxbits} a;
    signal input {nonzero, maxbits} b;
    signal output {maxbits} q <-- a \ b;    // maxbits enforced below
    signal output {maxbits} r <-- a % b;    // maxbits enforced below

    var N = a.maxbits;
    var M = b.maxbits;

    // We only need to test 2^M(2^N + 1) <= p, but no easy way to in circom.
    // Instead, we can test that a slighlty larger 2^{M + N + 1} < p.
    // We do lose the ability of using this with higher (N, M), but that's okay.
    _ = AssertBitsFitScalar(N + M +1);

    // Check Euclidean division theorem, including r \in [0, b)
    a === q * b + r;
    
    // We can assume b < 2^M, because it is tagged.
    // But, for the remainder r, we have to enforce it:
    // (Of course, r may is a bit smaller than M bits, because r < b)
    r.maxbits = M;
    _ <== Num2Bits(M)(r);
    
    // We know that r, b < 2^M => can safely call LessThan(M)
    signal valid_remainder <== LessThan(M)(r, b);
    valid_remainder === 1;
    
    // Check that q is bounded, otherwise we are in trouble
    q.maxbits = N;
    _ <== Num2Bits(N)(q);
}
```

{: .note}
You can see an implementation of a related template that computes just the remainder $r$, when a bound on $a$ is known, [here](https://github.com/zkFHE/circomlib-fhe/blob/96536373df183bc1849b96b0dc82f1a0f7a8abfd/circuits/mod.circom#L16).

### `AssertBitsFitScalar(N)` compile-time check

`circomlib` templates often hardcode the maximum # of bits that can fit into a $\Zp$ scalar by assuming $p$ to be the order of the BN254 curve.

This is not just **silly** as it may break applications deployed for different curves with higher $p$.

But it is also **insecure** for applications deployed with smaller $p$ than what the `circomlib` template asserts (e.g., what if somebody develops for Goldilocks with $p \approx 2^{64}$?).
If so, templates like [Num2Bits](#num2bitsn-template) would have serious soundness issues!

It would be better if we could check that **any** `N`-bit number "fits" in circom's configured $\Zp$ scalar type.

We do this via `AssertBitsFitScalar(N)` below, inspired by the `maxbits()` check in the [costa-group fork of circomlib](https://github.com/costa-group/circomlib/blob/83ee7b20df24a59e5c996c135cee23e6ffce5505/circuits/bitify.circom#L40).
```

/**
 * Computes the maximum bit-width $b$ such that an *unsigned* $2^b - 1$ value can
 * be always stored in a circom scalar in $\mathbb{Z}_p$, without it wrapping around
 * after being reduced modulo $p$.
 *
 * Leverages the fact that circom comparison operators treat scalars in $[0, p/2]$
 * as positive, while every $v \in (p/2, p)$ is treated as negative (i.e., as 
 * $v - p$ instead of as $v$).
 */
function MAX_BITS() {
    var n = 1;
    var b = 1;

    while (2 * n > n) {
        n = n * 2;
        b = b + 1;
    }

    return b;
}

/**
 * Utility method used to make sure an N-bit *unsigned* number will fit in a scalar,
 * for the curently-selected circom scalar field.
 */
function AssertBitsFitScalar(N) {
    var max_bits = MAX_BITS();
    log("N: ", N);
    log("MAX_BITS(): ", max_bits);

    assert(N <= max_bits);

    return 0; // circom needs you to return smth!
}
```

## TODOs

### Assert template parameters are not larger than field order

How do we assert non-overflows on variables like?
What if they exceed the field size?

```
var MAX_QUOTIENT = (3 * MAX_ENCODED_LEN) \ 4;
```

### `CalculateTotal`

Why do people add constraints to implement sums? Why don't they just use a `var`? (e.g., [here](https://rubydusa.medium.com/graceful-tree-labeling-in-circom-a-look-into-a-circom-circuit-89eccec31f61))

## Conclusion

`circom` is an amazing tools for implementing ZK relations that (obviously) makes the developer's job 10,000x easier than writing R1CS constraints directly.

I am still trying to understand the many pitfalls developers can fall into when writing `circom` code, most importantly, under-constraining bugs.

Looking forward to looking at [NoirLang](https://x.com/NoirLang) to see if it makes it easier to distinguish between constraints and witness generation?

## Awesome list

### Libraries

 - [iden3/circomlib](https://github.com/iden3/circomlib/tree/master/circuits), standard library from iden3
 - [costa-group/circomlib](https://github.com/costa-group/circomlib), a modified `circomlib` that leverages tags (see [diffs](https://github.com/iden3/circomlib/compare/master...costa-group:circomlib:master))
 - [zkFHE/circomlib-fhe](https://github.com/zkFHE/circomlib-fhe/tree/main/circuits)
 - [nullity00/circom-circuits](https://github.com/nullity00/circom-circuits/)
 - [Trees in circom](https://rubydusa.medium.com/graceful-tree-labeling-in-circom-a-look-into-a-circom-circuit-89eccec31f61)
 - [ABDK libraries for circom](https://github.com/abdk-consulting/abdk-libraries-circom)

### SoK

 - [zksecurity/zkbugs](https://github.com/zksecurity/zkbugs)
 - [0xPARC/zk-bug-tracker](https://github.com/0xPARC/zk-bug-tracker)
 - [nullity00/zk-security-reviews](https://github.com/nullity00/zk-security-reviews)
 - [pluto/circom-correctly-constrained](https://github.com/pluto/circom-correctly-constrained)
 - [Best Practices for Large circuits](https://hackmd.io/V-7Aal05Tiy-ozmzTGBYPA?view)

### Security list

#### Coda[^LKLplus23e]

 - [Veridise/coda](https://github.com/Veridise/Coda)
 - ZK DSL that allows developer to specify functional correctness theorems about the circuits they write
 - Compiles to R1CS
 - Somewhat-automated-but-not-fully: generates intermediate lemmas that have to be manually proved in Coq/Rocq

{: .todo}
Unclear whether/how it handles witness generation.

{: .todo}
Unclear how mature this is.

#### The `--inspect` option in circom itself

 - Helps find (some?) underconstraining bugs
    + How? Unclear.
 - Most important thing: marking unused signals via `_ <== unused_signal` to avoid false positives
 - See [example here](https://github.com/pluto/circom-correctly-constrained?tab=readme-ov-file#circom-docs---inspect-option)
 - See [circom docs here](https://docs.circom.io/circom-language/code-quality/inspect/)

#### circomspect[^circomspect]

 - Like `circom --inspect` above, but does more checks
 - It's called a "linter", but does not seem to actually lint like `cargo fmt` would

#### zkFuzz

 - [zkFuzz](https://github.com/Koukyosyumei/zkFuzz)

#### circomscribe[^circomscribe]

 - Allows you to paste in circom code and see what constraints are generated, in a developer friendly way. 
 - Likely does not scale to big circuits, due to cognitive overload.
 - Available to use for free [here](https://circomscribe.dev).
 - Does not seem to be open source

#### Ecne[^ecne]

 - Helps "verify that certain sets of R1CS constraints uniquely identify functions"
 - Takes as input just the .r1cs file (e.g., see [here](https://github.com/franklynwang/EcneProject/blob/25935352ca30705ad47363dcf200822635edc1c7/examples/ecdsa_secp_abstraction.jl#L4C9-L4C34))

{: .warning}
A bit confusing: is Ecne trying to solve two problems at the same time?
First, incorrect compilation from circom to R1CS.
Second, identify missed constraints in circom by looking directly at the compiled R1CS? A bit awkward.
Confusingly, its stated goal is _"to make it easier to convert functions into R1CS form"_, which presumably circom already solves? 

#### Picus[^PCWplus23e]

 - Detects _under-constraining bugs_: i.e., "a circuit is underconstrained if the equations do not specify a function."
    + Not sure how useful this is in reality.
    + While it seems very useful to know that a template is **a** function, it says nothing about it being the **right** function.
 - It is [open-source](https://github.com/Veridise/Picus)

### Others

Exercises:

 - [ZK exercises](https://hackmd.io/@gubsheep/S1Hz96Yqo)

Testing frameworks:

 - [erhant/circomkit](https://github.com/erhant/circomkit)

Audits:

 - [zkEmail audit, by MatterLabs](https://github.com/matter-labs-audits/reports/blob/main/research/zkemail/README.md)
 
## References

For cited works and other resources[^0xparc]$^,$[^tornadocash-circom]$^,$[^ecne]$^,$[^circom101]$^,$[^MBIplus22], see below ðŸ‘‡ðŸ‘‡

[^0xparc]: [0xPARC ZK bug tracker](https://github.com/0xPARC/zk-bug-tracker)
[^circom-stdlib]: [circom-stdlib](https://github.com/alinush/circom-stdlib), Alin Tomescu
[^circom101]: [Circom101](https://circom.erhant.me/) book
[^circomscribe]: [You like Circom but you find it confusing? Introducing Circomscribe](https://blog.zksecurity.xyz/posts/circomscribe/), zkSecurity
[^circomspect]: [Circomspect ðŸ”Ž](https://github.com/trailofbits/circomspect), Trail of Bits
[^ecne]: [Ecne](https://github.com/franklynwang/EcneProject)
[^Hess24]: [Tag, youâ€™re it: Signal tagging in Circom](https://blog.trailofbits.com/2024/01/02/tag-youre-it-signal-tagging-in-circom/), by Tjaden Hess, January 2nd, 2024
[^Pall23]: [A beginner's intro to coding zero-knowledge proofs](https://dev.to/spalladino/a-beginners-intro-to-coding-zero-knowledge-proofs-c56), by Santiago Palladino, May 4th 2023
[^tornadocash-circom]: [Breaking down TornadoCash](https://learn.0xparc.org/materials/circom/learning-group-1/breaking-down-tornado/), a live explanation of TorandoCash's `circom` implementation

<!-- Other interesting things:

 - https://github.com/pluto/circom-correctly-constrained?tab=readme-ov-file#example-3-further-examples
    + shows some non-intuitive automatic constraining happening
 - https://battlezips.gitbook.io/battlezips 
 - https://learn.0xparc.org/materials/circom/learning-group-1/circom-1
 - https://github.com/arnaucube/awesome-circom
 - a nice explanation by ChatGPT on how to test for `x === y` using just addition and multiplication by demanding to satisfy `(x-y) * inv === 1 - b`, where b is either 0 or 1: https://chatgpt.com/share/67d71175-8be8-8001-95cc-1ddec3c58b05
    + i guess if b is viewed as the output of a circuit that takes x and y as input (and inv as a hint input), then the verifier would constrain b to be 0 or 1 and this would force assigning inv correctly based on whether x == y
-->

{% include refs.md %}
